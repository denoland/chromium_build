# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rs_bindings_from_cc.gni")
import("//build/rust/rust_executable.gni")

rust_executable("test_rs_bindings_from_cc") {
  crate_root = "main.rs"
  sources = [ "main.rs" ]
  deps = [ ":self_contained_target_rs_api" ]
  build_native_rust_unit_tests = true
}

rs_bindings_from_cc("self_contained_target") {
  # TODO(crbug.com/1297592): Is there something we can do (a convention?) to
  # avoid duplication/repetition across
  # 1a) `public_headers` here and 1b) `sources` in ":self_contained_target"
  # 2) mentioning "self_contained_target" in `deps` and in the name of the
  #    target (with "_rs_api" suffix added).
  public_headers = [
    "self_contained_target_header1.h",
    "self_contained_target_header2.h",
  ]
}

source_set("self_contained_target") {
  sources = [
    "self_contained_target_header1.h",
    "self_contained_target_header2.cc",
    "self_contained_target_header2.h",
  ]
}
